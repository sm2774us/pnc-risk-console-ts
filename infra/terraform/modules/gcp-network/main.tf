variable "name" { type = string }
variable "region" { type = string }
variable "subnet_cidr" {
  type    = string
  default = "10.30.0.0/20"
}
variable "pods_cidr" {
  type    = string
  default = "10.31.0.0/16"
}
variable "services_cidr" {
  type    = string
  default = "10.32.0.0/20"
}

resource "google_compute_network" "this" {
  name                    = var.name
  auto_create_subnetworks = false
}
resource "google_compute_subnetwork" "this" {
  name                     = "${var.name}-subnet"
  region                   = var.region
  network                  = google_compute_network.this.id
  ip_cidr_range            = var.subnet_cidr
  private_ip_google_access = true
  secondary_ip_range {
    range_name    = "pods"
    ip_cidr_range = var.pods_cidr
  }
  secondary_ip_range {
    range_name    = "services"
    ip_cidr_range = var.services_cidr
  }
  log_config {
    aggregation_interval = "INTERVAL_10_MIN"
    flow_sampling        = 0.5
  }
}
resource "google_compute_router" "this" {
  name    = "${var.name}-router"
  region  = var.region
  network = google_compute_network.this.id
}
resource "google_compute_router_nat" "this" {
  name                               = "${var.name}-nat"
  router                             = google_compute_router.this.name
  region                             = var.region
  nat_ip_allocate_option             = "AUTO_ONLY"
  source_subnetwork_ip_ranges_to_nat = "ALL_SUBNETWORKS_ALL_IP_RANGES"
}
resource "google_compute_firewall" "deny_ingress" {
  name      = "${var.name}-deny-all-ingress"
  network   = google_compute_network.this.name
  direction = "INGRESS"
  priority  = 65000
  deny { protocol = "all" }
  source_ranges = ["0.0.0.0/0"]
}
output "network" { value = google_compute_network.this.id }
output "subnetwork" { value = google_compute_subnetwork.this.id }
